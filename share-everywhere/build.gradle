apply plugin: 'com.android.library'

dependencies {
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 21
        versionCode rootProject.ext.libraryVersionCode
        versionName rootProject.ext.libraryVersion
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            res.srcDirs = ['res']
        }
    }
}


// Publish to Bintray

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId rootProject.ext.bintrayGroupId
            artifactId rootProject.ext.bintrayArtifactId
            version rootProject.ext.libraryVersion

            artifact(javadocJar)
            artifact(sourceJar)
            artifact("$buildDir/outputs/aar/share-everywhere-release.aar")
        }
    }
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'maven'
        name = rootProject.ext.bintrayArtifactId
        version {
            name = rootProject.ext.libraryVersion
            vcsTag = rootProject.ext.libraryVersion
        }
    }
}
